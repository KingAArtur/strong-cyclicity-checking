\section{Приложение А \\ Исходные коды функций}


Функция для определения степени выражения, которое может содержать слагаемое вида $O(x^t)$
\begin{lstlisting}[basicstyle=\scriptsize]
max_degree := proc(p) :: integer;
    local result :: integer;
    
    if type(p, '`+`') or type(p, polynom) then
        # "polynom" - polynoms with or without "+", such as "x + 3x^3" or "4x^5"
        # "+" - polynoms with O(x^t) term, such as "2x + O(x^3)"
        if has(p, O) then
            # assuming that O(x^t) terms is max degree in the expression
            result := degree(op(select(has, p, O)), x) - 1;
        else
            if p = 0 then
                result := -1
            else
                result := degree(p, x)
            end if;
        end if;
    else
        # only O(x^t) term without any other terms, such as "O(x^4)"
        result := degree(op(p), x) - 1;
    end if;
    
    return result;
end proc:
\end{lstlisting}


\bigskip
Функция для построения продолжения многочлена
\begin{lstlisting}[basicstyle=\scriptsize]
prolong := proc(p :: polynom, coef, degree :: integer) :: polynom;
    local q;
    q := p + coef * x ^ degree;
    
    return q;
end proc:
\end{lstlisting}


\newpage
Функция для построения матрицы производных вектора в силу системы
\begin{lstlisting}[basicstyle=\scriptsize]
CV_dv := proc(A, v, m)
    local n, i, j, k, W, AT, width;
    
    n := LinearAlgebra[RowDimension](A);
    if _npassed > 2 then
        width := m
    else
        width := n
    end if;
    
    AT := LinearAlgebra[Transpose](A);
    W := Matrix(n, width);
    for i from 1 to n do
        W[i, 1] := v[i]
    end do;
    
    for j from 2 to width do
        for i from 1 to n do
            W[i, j] := simplify(diff(W[i, j - 1], x) + add(AT[i, k] * W[k, j - 1], k = 1 .. n))
        end do
    end do;
    
    return(W);
end proc:
\end{lstlisting}


\bigskip
Процедура, запускающая алгоритм проверки сильной цикличности вектора
\begin{lstlisting}[basicstyle=\scriptsize]
is_strong_cyclic := proc(A :: Matrix, v :: list, max_recursion_depth :: integer) :: boolean;
    local actual_max_recursion_depth, n, degree_matrix, A_copied, i, j;
    
    if _npassed <= 2 then
        actual_max_recursion_depth := MAX_RECURSION_DEPTH
    else
        actual_max_recursion_depth := max_recursion_depth
    end if;
    
    show("=====");
    show("Starting calculations...");
    show("max_recursion_depth" = actual_max_recursion_depth);
    show("=====");
    
    n := LinearAlgebra[RowDimension](A);
    
    # calculating degrees for each element and removing O-notation
    degree_matrix := Matrix(n);
    A_copied := LinearAlgebra[Copy](A);
    for i from 1 to n do
        for j from 1 to n do
            degree_matrix[i, j] := max_degree(A_copied[i, j]);
            A_copied[i, j] := remove_o_notation(A_copied[i, j]);
        end do;
    end do;
    
    # clearing result matrix
    RESULT_MATRIX := Matrix():
    
    # calling recursive procedure
    return is_strong_cyclic_process(A_copied, v, actual_max_recursion_depth, degree_matrix);
end proc:
\end{lstlisting}


Рекурсивная часть алгоритма проверки сильной цикличности вектора
\begin{lstlisting}[basicstyle=\scriptsize]
is_strong_cyclic_process := proc(A :: Matrix, v :: list, steps_left :: integer, degree_matrix :: Matrix) :: boolean;
    local n, A_copied, degree_matrix_copied, CV, det, i, j, k, c, root, roots, solutions, failed, result;

    show("=====");
    show("depth" = steps_left);
    show(A);
    
    if steps_left = 0 then
        show(FAIL);
        return FAIL;
    end if;
    
    n := LinearAlgebra[RowDimension](A);
    A_copied := LinearAlgebra[Copy](A);
    degree_matrix_copied := LinearAlgebra[Copy](degree_matrix);
    
    # finding all 1-element prolongations that make tr(det) = 0
    solutions := [];
    for i from 1 to n do
        for j from 1 to n do
            A_copied[i, j] := A_copied[i, j] + c * x ^ (degree_matrix_copied[i, j] + 1);
            CV := CV_dv(A_copied, v);
            det := sort(collect(LinearAlgebra[Determinant](CV), x), [x]);
            
            roots := find_roots(tcoeff(det, x), c);
            show(A_copied);
            show(det);
            show(roots);
            
            # checking all found roots and appending them to the list
            for k from 1 to numelems(roots) do
                if roots[k] <> 0 then
                    if eval(det, c=roots[k]) = 0 then
                        # prolongation was found! => not strong cyclic
                        A_copied[i, j] := A[i, j] + roots[k] * x ^ (degree_matrix_copied[i, j] + 1);
                        
                        RESULT_MATRIX := A_copied;
                        show("not cyclic!");
                        return false;
                    end if;
                    solutions := [op(solutions), [i, j, roots[k]]];
                end if;
            end do;
            
            # discarding changes for this element
            A_copied[i, j] := A[i, j];
        end do;
    end do;
    
    # recursively applying to all collected prolongations
    failed := false;
    for k from 1 to numelems(solutions) do
        i, j, root := solutions[k, 1], solutions[k, 2], solutions[k, 3];
        
        A_copied[i, j] := A_copied[i, j] + root * x ^ (degree_matrix_copied[i, j] + 1);
        degree_matrix_copied[i, j] := degree_matrix_copied[i, j] + 1;
        
        result := is_strong_cyclic_process(A_copied, v, steps_left - 1, degree_matrix_copied);
        degree_matrix_copied[i, j] := degree_matrix[i, j];
        A_copied[i, j] := A[i, j];
        
        if result = false then
            show("not cyclic!");
            return false;
        end if;
        if result = FAIL then
            failed := true;
        end if;
    end do;
    
    if failed then
        show("FAIL");
        return FAIL;
    else
        show("yes, strong cyclic!");
        return true;
    end if;
end proc:

\end{lstlisting}
